Flow io.jans.agamapasskey.main
    Basepath "agamapasskey"
    Configs conf    //MFA settings

Log "Identity agama-passkey started"

authService = Call io.jans.as.server.service.AuthenticationService#class
cdiUtil = Call io.jans.service.cdi.util.CdiUtil#bean authService
loginResponse = {}
Repeat 3 times max
    loginResponse = RRF "main.ftlh" loginResponse
    loginResponse.success = Call cdiUtil authenticate loginResponse.username loginResponse.password
    loginResponse.uid = loginResponse.username

    When loginResponse.success is true
        userData = Call io.jans.agamapasskey.IdentityProcessor#accountFromUid loginResponse.uid
        obj = Trigger io.jans.agamapasskey.credsEnrollment userData conf true null

        When obj.success is true
            When obj.data.hadEnoughCreds is false
                //let the user in, he just finished enrolling his creds
                Finish loginResponse.uid

            //Challenge the user to present his creds
            obj = Trigger io.jans.agamapasskey.authenticator userData obj.data.mfaInfo
            When obj.success is true
                Finish loginResponse.uid

        //Fail if credential enrollment or authenticator flow failed
        Finish obj

Finish false
